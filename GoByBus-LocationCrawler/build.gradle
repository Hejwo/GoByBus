// correct
apply plugin: 'java'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        def springBootVersion = "1.5.6.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:" + springBootVersion)
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

jar {
    baseName = 'GoByBus-LocationCrawler'
}

springBoot {
    mainClass = "org.hejwo.gobybus.locationcrawler.LocationCrawlerApp"
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
    integrationTestsCompile.extendsFrom testCompile
    integrationTestsRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTests {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTests/java')
        }
        resources.srcDir file('src/integrationTests/resources')
    }
}

test {
    testLogging.showStandardStreams = true
}

dependencies {
    compile project(':GoByBus-Commons')

    // Spring
    compile 'org.springframework.cloud:spring-cloud-starter-config'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'
    compile 'org.springframework.boot:spring-boot-starter-quartz:2.0.0.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-log4j:1.3.8.RELEASE'

    // Kafka
    compile 'org.apache.kafka:kafka_2.10:0.10.1.1'

    // Commons
    compile 'commons-io:commons-io:2.5'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'com.google.code.gson:gson:2.8.0'

    compile 'com.google.guava:guava:20.0'
    compile 'org.projectlombok:lombok:1.16.12'

    // Unit tests
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
    testCompile 'com.github.tomakehurst:wiremock-standalone:2.8.0'
    testCompile 'net.javacrumbs.json-unit:json-unit-fluent:1.19.0'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR7"
    }
}

task image(type: Docker, dependsOn: assemble){
    push = false
    tag = "${project.group}_${applicationName}".toLowerCase()
    dockerfile = file('docker/Dockerfile')
    doFirst{
        copy {
            from jar
            from 'docker'
            into stageDir
        }
    }
}

task buildImage() {
    group 'deployment'

    dependsOn 'clean'
    dependsOn 'build'
    dependsOn 'image'
    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('image').mustRunAfter 'build'
}

task reloadContainer(type: Exec) {
    group 'deployment'

    dependsOn 'killContainer'
    ignoreExitValue true
    workingDir '..'
    commandLine 'docker-compose', 'up', '-d', '--no-deps', jar.baseName.toLowerCase()
}

task killContainer(type: Exec) {
    group 'deployment'
    dependsOn 'buildImage'

    ignoreExitValue true
    workingDir '..'
    commandLine 'docker-compose', 'kill', jar.baseName.toLowerCase()
}

task integrationTests(type: Test) {
    group 'verification'
    testClassesDir = sourceSets.integrationTests.output.classesDir
    classpath = sourceSets.integrationTests.runtimeClasspath
}

check.dependsOn integrationTests
integrationTests.mustRunAfter test